plugins {
    id 'java'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'gen-java']
        }
    }
}

dependencies {
    // Dépendance essentielle pour les classes générées par Thrift
    implementation 'org.apache.thrift:libthrift:0.16.0'
    // Logger simple pour éviter le warning SLF4J (NOP binder)
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    // Servlet API for compilation (provided by container at runtime)
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
}

// Tâche : lancer le serveur Thrift InternalCRM
tasks.register('runInternalCRMServer', JavaExec) {
    group = 'application'
    description = 'Lance le serveur Thrift InternalCRM (port par défaut 9090).'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.example.internal.InternalCRMServer'
    if (project.hasProperty('port')) args project.property('port')
}

// Tâche : lancer une démo locale sans réseau (instancie juste le handler) A SUPP AVEC LE FICHIER DEMO
tasks.register('runInternalCRMDemo', JavaExec) {
    group = 'application'
    description = 'Exécute une démo locale du handler InternalCRM.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.example.internal.InternalCRMDemo'
}

